#/
##############################################################################
### Character Generation Bio Room  ###########################################

https://github.com/lcanady/CrescentMoonMux

This is the code for the bio section of character generation.

Requires:
    Global Room Parent <GRP>


------------------------------------------------------------------------------
--- @desc --------------------------------------------------------------------

The room @desc is broken up into 4 parts; The intro, the 'stat block',
directions and footer instrutctions.
/#



@desc here=
    %r%t[u(intro)]%r%r
    [repeat(%cr-%cn,78)]%r
    [u(#54/block.bio,%#,getstat(%#/template))]%r
    [repeat(%cr-%cn,78)]%r%r
    For more information about any of the stats here, type '%chstat <stat>%cn'.%r%r
    [u(bio.[getstat(%#/template)])]%r%r
    [u(.footer)]%r

-

@va here=#14
-
#/
------------------------------------------------------------------------------
--- Intro Text ---------------------------------------------------------------
/#

&intro here= In this room we'll set the details of the 'bio' section of your character's sheet. Once these details are set, we'll move onto setting up your character's attributes.
-


#/
------------------------------------------------------------------------------
--- .footer ------------------------------------------------------------------
/#

&.footer here=
    Type '%chNext%cn' to continue to [eval( default( me/next, the next step. ))]%r
    Type '%chBack%cn' to return to [eval( default( me/previous, the previous step. ))] %r
    Type '%chrestart%cn' to begin this step from the beginning again.%r
    Type '%chabort%cn' to leave character generation.
-

#/
------------------------------------------------------------------------------
--- Bio Blocks ---------------------------------------------------------------

Short lists on the commands needed to set bio entries of the character sheet.

/#

&bio.human here=
    %chFull Name:%cn stat/set Full Name=<Name>%r
    %chBirthdate:%cn stat/set Birthdate=<Month Day Year>%r
    %chVirtue:%cn stat/set Virtue=<Virtue>%r
    %chVice:%cn stat/set Vice=<Vice>%r%r
    %chGender:%cn @sex me=<Male/Female/yes/no/whatever>%r
-

&bio.vampire here=
    %chClan:%cn stat/set Clan=<Clan>%r
    %chCovenant:%cn stat/set Covenant=<Covenant>%r
    %chMask:%cn stat/set Mask=<Mask>%r
    %chDirge:%cn stat/set Dirge=<Dirge>%r
    %chEmbrace Date:%cn stat/set Embrace=<Month Day Year>%r
    %chMystery (OD Only):%cn stat/set Mystery=<Mystery>%r
    %chGender:%cn @sex me=<Male/Female/yes/no/whatever>%r
-

&bio.ghoul here=
    %chFull Name:%cn stat/set Full Name=<Name>%r
    %chBirthdate:%cn stat/set Birthdate=<Month Day Year>%r
    %chClan%cn: stat/set Clan=<Clan>%r
    %chRegnant%cn: stat/set Regnant=<Regnant>%r
    %chVirtue:%cn stat/set Virtue=<Virtue>%r
    %chVice:%cn stat/set Vice=<Vice>%r%r
    %chGender:%cn @sex me=<Male/Female/yes/no/whatever>%r
-

&bio.werewolf here=
    %chAuspice:%cn stat/set Auspice=<Auspice>%r
    %chTribe:%cn stat/set Tribe=<Tribe>%r
    %chBlood:%cn stat/set Blood=<Blood>%r
    %chBone:%cn stat/set Bone=<Bone>%r
    %chBirthdate:%cn stat/set Birthdate=<Month Day Year>%r
    %chLodge:%cn stat/set Lodge=<Lodge>%r
    %chGender:%cn @sex me=<Male/Female/yes/no/whatever>


#/
=============================================================================
=== Command: Restart ========================================================

Reset all of the bio fields except for template and concept.

/#

&c.restart here=$^\+?restart$:
    [setq( 0, filter( %!/filter.bio, lattr( %#/_BIO* )))];

    @dolist %q0={ &## %#=; };

    @pemit %#= [u( .msg, Chargen, Bio section cleared.)]
-
@set here/c.restart=regex
-

#/
-----------------------------------------------------------------------------
--- Filter.Bio --------------------------------------------------------------
/#

&filter.bio here=
    not(match(_BIO.CONCEPT _BIO.TEMPLATE _BIO.SPHERE, %0))
-

*/
-----------------------------------------------------------------------------
---- Exit Lock: Next <N> ----------------------------------------------------

There are certain things that /have/ to be set in this room, or they break
stuff later in chargen!

/*

@set n=inherit
-

@lock/defaultlock n = lock/1
-

&lock n =
    or(
        udefault( %!/lock.[getstat(%#/template)], 1, %# ),
        orflags(%#,wWZ)
    )
-

&lock.werewolf n=
        hasattr(%0,_bio.auspice)
-

@fail n=%ch<Chargen>%cn To continue forward the following must be set:%b
    %ch[v( bio.[getstat( %#/template )])]%cn

-

&bio.werewolf n = Auspice

#/
-----------------------------------------------------------------------------
--- .msg --------------------------------------------------------------------
/#

&.MSG here= ansi( h, <%0>, n, %b%1 )
