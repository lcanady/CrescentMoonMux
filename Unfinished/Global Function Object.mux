/*
################################################################################
### Global Function Object #####################################################

https://github.com/lcanady/CrescentMoonMux

Formatted for Muxify: http://muxify.musoapbox.net/editor.html
Shoutout to https://github.com/thenomain for his formatting.

This is the general globals object for Crescent Moon Mux. It holds helpful,
random global user functions. 

--------------------------------------------------------------------------------
--- @startup -------------------------------------------------------------------

Startup script to add user functions to the @function list. This is taken from
Thenomian (See link above!). 

*/

@Startup #56= 
    @dolist lattr( %!/ufunc.* )=
        @function/preserve [rest( ##, . )]=%!/##; 
    @dolist lattr( %!/ufunc/privileged.* )=
        @function/preserve/privileged [rest( ##, . )]=%!/##


/*
--------------------------------------------------------------------------------
--- f.convert.secs -------------------------------------------------------------

Formats seconds into d, h, m, s format.  This code was originally part of the
Global Sandbox Project function secs2hrs. I don't know the originial developer
please forgive.

0: Seconds idle

*/

&f.convert.secs #56=
    [switch( 1,
        lte( %0, 60 ), %0s,
        lte( %0, 3600 ),[div( %0, 60 )]m,
        lte( %0, 86400 ),
            [div( 1%0, 3600 )]h%b
            [rjust( div( mod( %0, 3600), 60), 2)]m,
        [div( %0, 86400) ]d%b
        [rjust( div( mod( %0, 86400), 3600), 2 )]h
    )]


/*
--------------------------------------------------------------------------------
--- f.idletime -----------------------------------------------------------------

Formats a player's idle time into d, h, n, s format for the Global Room Parent.

0: Player dbref

*/

&f.idletime #56= u( f.convert.secs, idle(%0) )


/*
--------------------------------------------------------------------------------
--- f.shortdesc ----------------------------------------------------------------

Formats a player's &short-desc attribute for the Global Room Parent.

0: Player dbref

*/

&f.shortdesc #56=
    ljust( 
        udefault( 
            %0/short-desc, 
            ansi( hx, Type: &short-desc me=<text>, n ) 
        ), 41 
    )
-


/*
--------------------------------------------------------------------------------
--- f.header -------------------------------------------------------------------

Outputs a pretty formatted header.

0: Text

*/

&f.header #56=
    center(
        ansi( hr, <<, h, %b%0%b, hr, >>, n ),78,%cr-%cn
    )


/*
--------------------------------------------------------------------------------
--- f.divider ------------------------------------------------------------------

Outputs a pretty formatted divider.

0: Text

*/

&f.divider #56=
    center( ansi( hr, <<, h, %b%0%b, hr, >>, n ), 78, ansi( r, -, n ))


/*
--------------------------------------------------------------------------------
--- f.footer -------------------------------------------------------------------

Outputs a pretty formatted footer. Takes an optional variable to add a text
portion to the output.

0: Text

*/

&f.footer #56 =
    strcat(
        rjust(
            if( %0, ansi( hr, <<, n, %b%0%b, hr, >>, n )),
            sub( 78, 3 ),
            ansi(r, -, n )
        ),
        ansi( r, ---, n )
    )


/*
--------------------------------------------------------------------------------
--- User Functions -------------------------------------------------------------
*/

&ufunc/privileged.idletime #56=u( #56/f.idletime, %0)
&ufunc/privileged.shortdesc #56=u( #56/f.shortdesc, %0)
&ufunc/privileged.header #56=u( #56/f.header, %0)
&ufunc/privileged.divider #56=u( #56/f.divider, %0 )
&ufunc/privileged.footer #56= u( #56/f.footer, %0 )