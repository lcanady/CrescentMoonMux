#/
#############################################################################
### Mux Weather System ######################################################


OUTPUT:

---<< Current Weather Conditions >>-------------------------------------------
  Current Conditions:                  ###############
  Temperature:                         ###############
  Wind:                                ###############
  Atmospheric Pressure:                ###############
  Visibility:                          ###############
  Humidity:                            ###############

  Sunrise:                             ###############
  Sunset:                              ###############
  Moon Phase:                          ###############
-------------------------------<< CURRENT TIME: Mon Aug 20 13:43:26 2018 >>---

#/
=============================================================================
=== SETUP ===================================================================
/#

@create Weather System <WS>
@set  Weather System <WS>=inherit safe
@fo me=&d.ws me=search( name=Weather System <WS> )

#/
=============================================================================
=== Command: Weather ========================================================
/#

&c.weather [v( d.ws )]=$^\+?weather$:
    @pemit %#=
        [lheader(Current Weather Conditions)]
        %r[ljust(%b%b%chCurrent Conditions:%cn,38)][u( f.get-field, conditions, conditions )]
        %r[ljust(%b%b%chTemperature:%cn,38)][u( f.get-field, conditions, temperature )]
        %r[ljust(%b%b%chWind:%cn,38)][u( f.get-field, conditions, wind )]
        %r[ljust(%b%b%chAtmospheric Pressure:%cn,38)][u( f.get-field, conditions, pressure )]
        %r[ljust(%b%b%chVisibility:%cn,38)][u( f.get-field, conditions, visibility )]
        %r[ljust(%b%b%chHumidity:%cn,38)][u( f.get-field, conditions, humidity )]
        %r[ljust(%b%b%chSunrise:%cn,38)][timefmt($H:$M ,u( f.get-field, conditions, sunrise ))]
        %r[ljust(%b%b%chSunset:%cn,38)][timefmt($H:$M ,u( f.get-field, conditions, sunset ))]
        %r[ljust(%b%b%chMoon Phase:%cn,38)][u( f.get-field, conditions, moon_phase )]
        [if(
            u( f.is-moon-visible? ),
            %b<%chVisibile%cn>,
            %b<Not Visible>
        )]
        %r[footer(CURRENT TIME: [time()])]



#/
============================================================================
=== FUNCTIONS ==============================================================
/#

#/
-----------------------------------------------------------------------------
--- f.get-field -------------------------------------------------------------

0: Which 'help' file? (conditions)
1: Which 'field' (temperature)

Thenomian's code

/#

&f.get-field [v( d.ws )]=
    strcat(
        null( regmatchi( textfile( meteo, %0 ), %1: .+, 0 )),
        trim( rest( %q0, : ))
    )
-
#/
-------------------------------------------------------------------------------
-- Between --------------------------------------------------------------------

    u( .between, <value>, <num1>, <num2> )

Is y <= a <= x?
That is: Is 'a' between x and y, including x or y?

Also written by Thenomian!

/#

&.between [v( d.ws )]=
    cand(
        gte( %2, min( %0, %1 )),
        lte( %2, max( %0, %1 ))
    )

#/
-----------------------------------------------------------------------------
--- f.is-moon-visible? ------------------------------------------------------

Checks to see if it's between moonrise and moonset.

/#
&f.is-moon-visible? [v( d.ws )]=
    u( .between,
        secs(),
        u( f.get-field, conditions, moonrise ),
        u( f.get-field, conditions, moonset )
    )
