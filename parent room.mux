/*
################################################################################
### Global Room Parent #########################################################

Formatted for Muxify: http://muxify.musoapbox.net/editor.html

Usage
    @parent <dbref>=#16

This is the parent room for Crescent Moon Mux. It's set through the .config file
as parent_room and should be automatically set when new rooms are created. 

Requirements:
    header(<text>):         Game's pretty header function.
    idletime(<player>):     Checks a player's idle time.
    shortdesc(<player>):    Display a player's &short-desc attribute 


--------------------------------------------------------------------------------
--- Name Format ----------------------------------------------------------------

Format the room name to show the game header.  If the looker is staff/wizard or
the owner of the room can see the dbref and flags list.

*/

@NameFormat #16=
     header(
            [name(%!)]
            [if(
                or(match(%#,owner(%!)),orflags(%#,wWZ)),
                %([num(me)][flags(me)]%)
            )] 
     )


/*
--------------------------------------------------------------------------------
--- Content Format -------------------------------------------------------------

Room contents are split up into two parts, players and things.

*/

@ConFormat #16=     
    [u( .header, Characters )]
    [iter(  
        filter( filter.players, %# %0),
        %r[u( format.character, ## )]
    )]
    [if( 
        words( filter( filter.objects, %0) ),
        strcat(
            %r,u( .header, Objects ),
            iter( filter( filter.objects, %0 ), %r[name( ## )])
        )
    )]


/*
--------------------------------------------------------------------------------
--- Exit Format ----------------------------------------------------------------

Formats the room's exit list. 

Basic exit format. Don't worry about color, it's applied automatically:
    Exit Name <EN>;exit;en

Exits can have two optional attributes:
    &secondary <exit> = 1
    &tertiary <exit> = 1

*/

@ExitFormat #16= 
    [u( .header, Exits )]%r
    [columns(
        iter( %0, 

            // Check for secondary and tertiary exits.

            switch( 1,                  
                hasattr( ##, secondary ), 
                    edit( name(##), <, %ch%cc<%cn, >, %ch%cc>%cn),
                hasattr(##,tertiary),
                    edit(name(##),<,%ch%cm<%cn,>,%ch%cm>%cn),
                edit(name(##),<,%ch%cr<%cn,>,%ch%cr>%cn)
            ),,|
        )
        ,26,|
    )]
    [if(
        not( hasflag( %!, IC )),
        footer( OOC AREA ),
        footer()
    )]


/*
--------------------------------------------------------------------------------
--- Filter Players -------------------------------------------------------------

Filters players in the room.

filter( filter.players, %0 )

0: contents list

*/

&filter.players #16=
    and(
        hastype( %0, player ),
        not( hasflag( %0, dark ))
    )


/*
--------------------------------------------------------------------------------
--- Filter Objects -------------------------------------------------------------

Filters objects in the room.

filter( filter.objects, %0 )

0: contents list

*/

&filter.objects #16=
    hastype( %0, thing )

/*
--------------------------------------------------------------------------------
--- .header --------------------------------------------------------------------

Shows a formatted header for @conformat and @exitformat

0: Title

*/
    

&.header #16=
    strcat(
        ansi( r, ---, n ),
        ljust( 
            ansi( hr, <<, h, %b%0%b, hr, >>, n ), 
            sub(78,3), 
            ansi(r,-,n)
        )
    )

/*
--------------------------------------------------------------------------------
--- Format Moniker -------------------------------------------------------------

Checks to see if a player has a moniker.  If looker is wizard or staff, it will 
also show the dbref of a player.

0: target
1: looker 

*/

&format.moniker #16=
        [moniker(%0)][if(orflags(%1,wW),%(%0%))]


/*
--------------------------------------------------------------------------------
--- Format Character -----------------------------------------------------------

Formats character information for display to the room.

0: target
1: looker 

*/

&format.character #16=
    cat(
        ljust( u( format.moniker, %0, %1 ) ,22),
        ljust( idletime(%0), 8 ),
        if(hasattr( %0, sex ), %([ucstr( mid( get(%0/sex),0 ,1 ))]%)),
        shortdesc(%0)
    )
